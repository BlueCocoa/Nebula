ENTRY(_start)

SECTIONS
{
    . = 0x8000;                     /* 将当前地址设置为0x8000, 即initsys开始执行的地方 */
    __start = .;

    __text_start = .;
    .text : {
        KEEP(*(.init))
        *(.text*)
        objects/initsys.o (.text* .data* .rodata* .bss*)
        KEEP(*(.system-wide))
    }
    . = ALIGN(4096);                /* 与页对齐 */
    __text_end = .;

    __rodata_start = .;
    .rodata : {
        *(.rodata)
    }
    . = ALIGN(4096);                /* 与页对齐 */
    __rodata_end = .;

    __data_start = .;
    _frame_data_start = .;
    .data : {
        *(.rawdata)
        *(.data)
    }
    . = ALIGN(4096);                /* 与页对齐 */
    __data_end = .;

    __bss_start = .;
    .bss : {
        bss = .;
        *(.bss)
    }
    . = ALIGN(4096);                /* 与页对齐 */
    __bss_end = .;

    __end = .;
}
